#!/usr/bin/env just
 
# --- Settings --- #
set shell                     := ["nu", "-c"]
set positional-arguments      := true
set allow-duplicate-variables := true
set windows-shell             := ["nu", "-c"]
set dotenv-load               := true

# --- Variables --- #
project_root      := justfile_directory()
output_directory  := project_root + "/dist"
build_directory   := `cargo metadata --format-version 1 | jq -r .target_directory`
system            := `rustc --version --verbose |  grep '^host:' | awk '{print $2}'`
main_package      := "${MAIN_BINARY}"

# â–°â–°â–°â–°â–°â–°â–°â–°â–°â–°â–°â–°â–°â–°â–°â–°â–° #
#      Recipes      #
# â–°â–°â–°â–°â–°â–°â–°â–°â–°â–°â–°â–°â–°â–°â–°â–°â–° #

[doc('List all available recipes')]
default:
    @just --list

# --- Build & Check --- #
[doc('Check workspace for compilation errors')]
[group('build')]
check:
    @echo "ğŸ” Checking workspace..."
    cargo check --workspace

[doc('Build workspace in debug mode')]
[group('build')]
build target="aarch64-apple-darwin" package=(main_package):
    @echo "ğŸ”¨ Building workspace (debug)..."
    cargo build --workspace --bin '{{ main_package }}' --target '{{ target }}'

[doc('Build workspace in release mode')]
[group('build')]
build-release target=(system) package=(main_package):
    @echo "ğŸš€ Building workspace (release) for {{ target }}â€¦"
    cargo build --workspace --release --bin '{{ main_package }}' --target '{{ target }}'

# --- Packaging --- #
[doc('Package release binary with completions for distribution')]
[group('packaging')]
package target=(system):
### IMPORT: scripts/package.nu 1 ###

[doc('Generate checksums for distribution files')]
[group('packaging')]
checksum directory=(output_directory):
### IMPORT: scripts/checksum.nu 1 ###

[doc('Compress all release packages into tar.gz archives')]
[group('packaging')]
compress directory=(output_directory):
### IMPORT: scripts/compress.nu 1 ###

[doc('Complete release pipeline: build, checksum, and compress')]
[group('packaging')]
release: build-release
    just checksum

# --- Execution --- #
[doc('Run application in debug mode')]
[group('execution')]
run package=(main_package) +args="":
    @echo "â–¶ï¸ Running {{ package }} (debug)..."
    cargo run --bin '{{ package }}' -- '$@'

[doc('Run application in release mode')]
[group('execution')]
run-release package=(main_package) +args="":
    @echo "â–¶ï¸ Running '{{ package }}' (release)..."
    cargo run --bin '{{ package }}' --release -- '$@'

# --- Testing --- #
[doc('Run all workspace tests')]
[group('testing')]
test:
    @echo "ğŸ§ª Running workspace tests..."
    cargo test --workspace

[doc('Run workspace tests with additional arguments')]
[group('testing')]
test-with +args:
    @echo "ğŸ§ª Running workspace tests with args: '$@'"
    cargo test --workspace -- '$@'

# --- Code Quality --- #
[doc('Format all Rust code in the workspace')]
[group('quality')]
fmt:
    @echo "ğŸ’… Formatting Rust code..."
    cargo fmt 

[doc('Check if Rust code is properly formatted')]
[group('quality')]
fmt-check:
    @echo "ğŸ’… Checking Rust code formatting..."
    cargo fmt 

[doc('Lint code with Clippy in debug mode')]
[group('quality')]
lint:
    @echo "ğŸ§¹ Linting with Clippy (debug)..."
    cargo clippy --workspace -- -D warnings

[doc('Automatically fix Clippy lints where possible')]
[group('quality')]
lint-fix:
    @echo "ğŸ©¹ Fixing Clippy lints..."
    cargo clippy --workspace --fix --allow-dirty --allow-staged

# --- Documentation --- #
[doc('Generate project documentation')]
[group('common')]
doc:
    @echo "ğŸ“š Generating documentation..."
    cargo doc --workspace --no-deps

[doc('Generate and open project documentation in browser')]
[group('common')]
doc-open: doc
    @echo "ğŸ“š Opening documentation in browser..."
    cargo doc --workspace --no-deps --open

# --- Maintenance --- #
[doc('Extract release notes from changelog for specified tag')]
[group('common')]
create-notes raw_tag outfile changelog:
### IMPORT: scripts/create-notes.nu 1 ###

[doc('Update Cargo dependencies')]
[group('maintenance')]
update:
    @echo "ğŸ”„ Updating dependencies..."
    cargo update

[doc('Clean build artifacts')]
[group('maintenance')]
clean:
    @echo "ğŸ§¹ Cleaning build artifacts..."
    cargo clean

# --- Installation --- #
[doc('Build and install binary to system')]
[group('installation')]
install package=(main_package): build-release
    @echo "ğŸ’¾ Installing {{ main_package }} binary..."
    cargo install --bin '{{ package }}'

[doc('Force install binary')]
[group('installation')]
install-force package=(main_package): build-release
    @echo "ğŸ’¾ Force installing {{ main_package }} binary..."
    cargo install --bin '{{ package }}' --force

# --- Aliases --- #
alias b    := build
alias br   := build-release
alias c    := check
alias t    := test
alias f    := fmt
alias l    := lint
alias lf   := lint-fix
alias cl   := clean
alias up   := update
alias i    := install
alias ifo  := install-force
alias rr   := run-release
